/*
   Copyright 2016 Thomas Nappo

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
 */

buildscript {
    ext.project_name = 'Arrowhead'
    ext.project_artifact = 'arrowhead'
    ext.project_version = '1.0'
    ext.kotlin_version = '1.0.3'
    ext.jna_version = '4.3.0-SNAPSHOT' // living on the bleeding edge!

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

group "org.jire.$project_artifact"
version project_version

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'kotlin'
}

publishing {
    publications {
        publish(MavenPublication) {
            from components.java
            groupId 'org.jire'
            artifactId project_artifact
            version project_version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['publish']
    pkg {
        repo = 'maven'
        name = project_name
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
        websiteUrl = "https://github.com/Jire/$project_name"
        issueTrackerUrl = "https://github.com/Jire/$project_name/issues"
        vcsUrl = "https://github.com/Jire/$project_name.git"
        githubRepo = "Jire/$project_name"
        version {
            name = project_version
            vcsTag = project_version
            gpg {
                sign = true
            }
        }
    }
}

repositories {
    mavenCentral()

    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile group: 'net.java.dev.jna', name: 'jna', version: jna_version
    compile group: 'net.java.dev.jna', name: 'jna-platform', version: jna_version

    compile group: 'it.unimi.dsi', name: 'fastutil', version: '7.0.12'
}